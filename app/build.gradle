apply plugin: 'com.android.application'
apply from: 'checkstyle.gradle'
apply from: 'cpd.gradle'
apply from: 'jacoco.gradle'
apply from: 'utils.gradle'

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'
    defaultConfig {
        applicationId 'com.smlnskgmail.jaman.hashchecker'
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 92
        versionName '3.6.7'
        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner 'com.smlnskgmail.jaman.hashchecker.runner.AndroidJacocoTestRunner'

        resConfigs 'en',
                'de',
                'el',
                'es',
                'fa',
                'fr',
                'hu',
                'it',
                'iw',
                'ko',
                'nl',
                'pl',
                'ru',
                'sv',
                'zh-rCN',
                'vi',
                'ja'
    }
    signingConfigs {
        release {
            def env = System.getenv();
            if (env['CI'] && env['GITHUB_WORKFLOW'] == 'Release') {
                final KEYSTORE_PATH = 'RELEASE_KEYSTORE_PATH'
                final KEYSTORE_PASSWORD = 'RELEASE_KEYSTORE_PASSWORD'
                final ALIAS_KEY = 'RELEASE_KEY_ALIAS'
                final PASSWORD_KEY = 'RELEASE_KEY_PASSWORD'
                def keystoreProperties = new Properties()
                def keystorePropertiesFile = rootProject.file("local.properties")
                if (keystorePropertiesFile.exists()) {
                    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
                }
                if (keystoreProperties[KEYSTORE_PATH] == null) {
                    keystoreProperties.put(
                            KEYSTORE_PATH,
                            env[KEYSTORE_PATH]
                    )
                    keystoreProperties.put(
                            KEYSTORE_PASSWORD,
                            env[KEYSTORE_PASSWORD]
                    )
                    keystoreProperties.put(
                            ALIAS_KEY,
                            env[ALIAS_KEY]
                    )
                    keystoreProperties.put(
                            PASSWORD_KEY,
                            env[PASSWORD_KEY]
                    )
                }
                if (!keystoreProperties.isEmpty()) {
                    storeFile file(keystoreProperties[KEYSTORE_PATH])
                    storePassword keystoreProperties[KEYSTORE_PASSWORD]
                    keyAlias keystoreProperties[ALIAS_KEY]
                    keyPassword keystoreProperties[PASSWORD_KEY]
                }
            }
        }
    }
    buildTypes {
        debug {
            versionNameSuffix '-dev'
            minifyEnabled false

            testCoverageEnabled = true
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'

            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main {
            resources.srcDirs = [
                    'src/main/resources',
                    'src/test'
            ]
        }
    }
    testOptions {
        animationsDisabled true

        unitTests {
            includeAndroidResources = true
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def buildType = variant.buildType.name
            output.outputFileName = "hash-checker_${buildType}.apk"
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.preference:preference:1.1.1'

    // SHA-3
    implementation 'com.github.aelstad:keccakj:1.1.0'

    // AdaptiveRecyclerView
    implementation 'com.github.fartem:adaptive-recycler-view:1.1.1'

    // Dagger
    implementation 'com.google.dagger:dagger:2.34.1'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.34.1'
    androidTestAnnotationProcessor 'com.google.dagger:dagger-compiler:2.34.1'

    // ORMLite
    implementation 'com.j256.ormlite:ormlite-core:5.3'
    implementation 'com.j256.ormlite:ormlite-android:5.3'

    // Checkstyle
    checkstyle 'com.github.fartem:checkstyle-checks-java:1.1.0'
    checkstyle 'com.github.fartem:checkstyle-checks-android:1.2.3'

    // Unit tests
    testImplementation 'junit:junit:4.13.1'

    // Android tests
    //noinspection GradleDependency
    androidTestImplementation 'androidx.annotation:annotation:1.1.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.4.0'

    // Screenshots
    androidTestImplementation 'tools.fastlane:screengrab:2.0.0'
}
